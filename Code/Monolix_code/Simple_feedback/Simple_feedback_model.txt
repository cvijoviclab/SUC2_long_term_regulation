DESCRIPTION: The simple-feedback model for describing the SUC2 dynamics 

Variables:
- SNF1_p, the SNF1-pathway activity on SUC2 
- X-unknown potential cascade of events (representing cellular response to starvation)
- SUC2, the SUC2 promoter activity measured via YFP 

 [LONGITUDINAL]
input = {k1, k3, k4, k5, k7, k8, k9, SUC20, tau_X}

EQUATION:
; Initial conditions 
SNF1p0 = 1
t0 = 0
SNF1p_0 = 1
SUC2_0 = SUC20
X_0 = 0
tau_SUC2 = 32

; Capturing the external glucose shift 
if t < 0.0483
   rate_in = k1
   HX = 0
else
   HX = 1
   rate_in = k1 / 40
end

; Equations derived from assuming a steady state 
k6 = k4 / ((k5 + SNF1p0*SNF1p0) * SUC20)
k2 = k1 / SNF1p0

; Dynamics, tax_X = tau-feedback 
ddt_SNF1p = rate_in - k2*SNF1p + k3 * delay(X, tau_X)
ddt_SUC2 = k4 / (k5 + delay(SNF1p, tau_SUC2)^2) - k6 * SUC2  
ddt_X = HX * k7 / (k8 + SNF1p) - k9 * X

OUTPUT:
output = {SUC2}
