DESCRIPTION: The simple-feedback model for describing the SUC2 dynamics 

Variables:
- SNF1_p, the SNF1-pathway activity on SUC2 
- X-unknown potential cascade of events (representing cellular response to starvation)
- SUC2, the SUC2 promoter activity measured via YFP 

 [LONGITUDINAL]
input = {k1,k2, k3, k4, k5,k6, k7, k8, k9, tau_X}

EQUATION:
; Equations derived from assuming a steady state 
SNF10 = k1 / k2
SUC20 = k4 / ((k5 + SNF10^1)*k6)
X0 = 0
; Initial conditions 
t0 = 0
SNF1p_0 = SNF10
SUC2_0 = SUC20
X_0 = X0
tau_SUC2 = 32

; Capturing the external glucose shift 
if t < 0.0483
   rate_in = k1
   HX = 0
else
   HX = 1
   rate_in = k1 / 40
end


; Dynamics, tax_X = tau-feedback 
ddt_SNF1p = rate_in - k2*SNF1p + k3 * delay(X, tau_X)
ddt_SUC2 = k4 / (k5 + delay(SNF1p, tau_SUC2)^1) - k6 * SUC2  
ddt_X = HX * k7 / (k8 + SNF1p) - k9 * X

OUTPUT:
output = {SUC2}
