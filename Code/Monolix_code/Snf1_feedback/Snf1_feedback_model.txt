DESCRIPTION: The simple-feedback model for describing the SUC2 dynamics 

Variables:
- SNF1p, phosphorylated SNF1 
- X, feedback component that acts on SNF1 
- SUC2, the SUC2 promoter activity measured via YFP
- Mig1, dephosphorylated nulcear Mig1
- Mig1p, phosphorylated nuclear Mig1 


[LONGITUDINAL]
input = {k1, k2, k3, k4, k5, k6, k7, k8, k9, k10}

EQUATION:
; From ensuring a steady-state 
Mig10 = k2 / k5
SUC20 = k6 / ((0.1 + Mig10^1)*k7)
X0 = 0


; Initial conditions
Mig1p0 = 0.0
t0 = 0
Mig1_0 = Mig10
Mig1p_0 = Mig1p0
SUC2_0 = SUC20
X_0 = X0
SNF1p_0 = 0

max_sig =3

; Assuming 32 minutes maturation time 
tau_SUC2 = 32

; Capturing the external glucose shift 
if t < 0.0483
   rate_down = k1
   HX = 0 
else
   rate_down = k1 / 40.0 * SNF1p
   HX = 1 
end


; Dynamics 
ddt_SNF1p = k1 - rate_down  - k10 * SNF1p * X
ddt_Mig1 = k2 - k3*SNF1p*Mig1 + k4*Mig1p - k5*(1 + 1 /(1 + exp(-max_sig * (SNF1p-4.5/max_sig) ) ) ) * Mig1 
ddt_Mig1p = k3*SNF1p*Mig1 - k4*Mig1p - k5*(1 + 1 /(1 +  exp(-max_sig * (SNF1p-4.5/max_sig) ) ) ) * Mig1p
ddt_SUC2 = k6 / (0.1+ delay(Mig1, tau_SUC2)^1) - k7 * SUC2
ddt_X =  k8 * (SUC2-SUC20)  - k9*X


OUTPUT:
output = {SUC2}
